container PopAge_disagg
{
	/* 
		Population age disaggregation method used to break down LUISA population base map into population fractions. 
		Setup by Chris Jacobs-Crisioni, data prepared and method devised in conjunction with Filipe Batista-Silva and Konstantin Rosina.
		The required order in which to generate maps:
		1) compute PopLayers, which consist of population fractions given finest available administrative units
		2) execute iterative proportional fitting procedures per NUTS3 (fit_proportionally/[NUTS3_Code]/Store_IFP_results)
		3) merge results from 2 using fit_proportionally/merge_maps (stores automatically)
	*/
	
	unit<dpoint> LambertEA: expr = "Geography/LambertEA";
	
	unit<uint32> Ageclasses: NrOfRows =  18, Descr = "Definition of narrow (5y) age classes that are allocated separately" {
		attribute<uint32> from: 		expr = "id(.) * 5", descr = "start of age class";
		attribute<uint32> to:			expr = "id(.) < max(id(.)) ? ((id(.) + 1) * 5) - 1 : 150", descr = "end of age class";
		attribute<string> padfrom: 		expr = "(from < 10 ? '0' : '') + string(from)", descr = "padded start of age class";
		attribute<string> padto:		expr = "(to < 10 ? '0' : '') + string(to)", descr = "padded end of age class";
		attribute<string> estat_notf:	expr = "switch(case(id(.) = 0, 'Y_LT5'), case(id(.) = max(id(.)), 'Y_GE85'), 'Y'+string(from)+'-'+string(to))", descr = "eurostat textual definition of age classes";
		attribute<string> LabelText:	expr = "id(.) < max(id(.)) ? 'y'+string(from) + 't'+string(to) : 'Over85'", descr = "descriptive name of age class";
		attribute<BroadClasses> broadfrom:		expr = "classify(from, BroadClasses/from)", descr = "link with broad age classes";
		attribute<EurostatClaims> eurostatfrom:	expr = "classify(from, EurostatClaims/from)", descr = "link with eurostat claim age classes";
		attribute<float32>	avage:		expr = "id(.) < max(id(.)) ? float32(from + to + 1) / 2f : float32(from) + 5f", descr = "average age in age class (assumed equal distribution)";
		attribute<float32>	IntraRegionalMobility: 	expr = "IntraRegionalMobility_bc[eurostatfrom]", descr = "assumed annual intra regional mobility in age class (static, obtained from census 2011)";
		attribute<float32>  ClassDuration: 			expr = "id(.) < max(id(.)) ? float32(to - from) + 1f : 0f / 0f", descr = "length of age class, null if last age class";
		attribute<float32>	IntraRegionalMobility_bc (EurostatClaims): [0.038366992, 0.066199885, 0.03884331, 0.017948642, 0.014092971], descr = "assumed annual intra regional mobility in age class (static, obtained from census 2011), in a broad age group definition";
		attribute<float32>	IntraRegionalMobility_no_u (EurostatClaims): expr = "const(0f,EurostatClaims,float32)", descr = "assumed zero intra regional mobility (used for testing purposes)";
		
		attribute<float32> Urban_att: 			[1,1,2,2,2,2,1.15720158759832,1,1,2,1,1,1,1,1,1.7283296818342,1,1], descr = "attractiveness of degurba = urban, rescaled from net migration rates";
		attribute<float32> Intermediate_att: 	[1.0496122328333,1.49006881785662,1.64853875851297,1.69647072706806,1.41543240031316,1.5135732132799,1,2,1.62875866921155,1.41455307785412,1.12129970198168,1.78787710238876,1.6373127694263,1.47687937324297,1.50540487715422,2,1.67417270498771,1.57808630632242], descr = "attractiveness of degurba = intermediate, rescaled from net migration rates";
		attribute<float32> Rural_att: 			[2,2,1,1,1,1,2,1.90356929938043,2,1,2,2,2,2,2,1,2,2], descr = "attractiveness of degurba = rural, rescaled from net migration rates";
	}
	
	unit<uint32> BroadClasses: NrOfRows = 6, descr = "Definition of broad (15y) age classes that are instrumental in parts of the procedure" {
		attribute<uint32> from: 		[0, 15, 30, 50, 65, 85], descr  = "start of age class";
		attribute<uint32> to:			expr = "id(.) < max(id(.)) ? from[id(.)+1] : 150", descr = "end of age class";
		attribute<string> LabelText:	expr = "switch(case(id(.) = 0, 'Under15'), case(id(.) = max(id(.)), 'Over85'), 'y'+string(from)+'t'+string(to-1))", DialogType = "LabelText", descr = "descriptive name of age class";
		attribute<string> AgeClassAdd:	expr = "AsItemList(Ageclasses/LabelText, AgeClasses/broadfrom)", descr = "rule governing which narrow age classes need to be added to constitute a broad age class";
	}
	unit<uint32> EurostatClaims: NrOfRows = 5, descr = "Definition of broad (15y) age classes as were defined in a previous version of the imputed Eurostat claims. These were instrumental in parts of the procedure, no longer used" {
		attribute<uint32> from: 		[0, 15, 30, 50, 65], descr = "start of age class";
		attribute<uint32> to:			expr = "id(.) < max(id(.)) ? from[id(.)+1] : 150", descr = "end of age class";
		attribute<string> LabelText:	expr = "switch(case(id(.) = 0, 'Under15'), case(id(.) = max(id(.)), 'Over65'), 'y'+string(from)+'t'+string(to-1))", DialogType = "LabelText", descr = "descriptive name of age class";
		attribute<string> AgeClassAdd:	expr = "AsItemList(Ageclasses/LabelText, AgeClasses/EuroStatFrom)", descr = "rule governing which narrow age classes need to be added to constitute a broad age class";
	}
	unit<uint32> Gender: NrOfRows = 3, descr = "Sex classes" {
		attribute<string> LabelText: 	['F','M','T'], DialogType = "LabelText", descr = "descriptive name of gender class";
	}
	unit<uint8>  DegUrba_lv1: NrOfRows = 3, descr = "Degree of urbanization level 1 classes" {
		attribute<string> LabelText: ['Urban', 'Intermediate', 'Rural'], descr = "descriptive name of degurba class";
		attribute<uint8>  Code:		expr = "id(.) + uint8(1)", descr = "coding in data";
	}
	#include<Units.dms>
	#include<Classifications.dms>
	#include<Geography.dms>
	#include<Boundaries.dms>
	#include<Reference_data.dms>
	#include<LUISA_sequence.dms>
	#include<Templates.dms>
	

	
}